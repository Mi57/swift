// RUN: %target-sil-opt -copy-propagation -enable-sil-opaque-values -enable-sil-verify-all %s | %FileCheck %s

sil_stage canonical

import Builtin
import Swift

// CHECK-LABAL: sil @testVarAssign : $@convention(thin) <T> (@in T) -> @out T {
// CHECK: bb0(%0 : $T):
// CHECK:   [[LOCAL:%.*]] = alloc_stack $T, var, name "u"
// CHECK:   store %0 to [init] [[LOCAL]] : $*T
// CHECK:   destroy_addr [[LOCAL]] : $*T
// CHECK:   dealloc_stack [[LOCAL]] : $*T
// CHECK:   return %0 : $T
// CHECK-LABEL: } // end sil function 'testVarAssign'
sil @testVarAssign : $@convention(thin) <T> (@in T) -> @out T {
bb0(%0 : $T):
  %2 = alloc_stack $T, var, name "u"
  %3 = copy_value %0 : $T
  store %3 to [init] %2 : $*T
  destroy_addr %2 : $*T
  dealloc_stack %2 : $*T
  destroy_value %0 : $T
  return %3 : $T
}

sil @multiResult : $@convention(thin) <T> (@in T) -> (@out T, @out T) {
// %0                                             // users: %3, %2, %1
bb0(%0 : $T):
  debug_value %0 : $T, let, name "t", argno 1     // id: %1
  %2 = copy_value %0 : $T                         // user: %3
  %3 = tuple (%2 : $T, %0 : $T)                   // user: %4
  return %3 : $(T, T)                             // id: %4
}

sil @callMultiResult : $@convention(thin) <T> (@in T) -> @out T {
bb0(%0 : $T):
  %2 = copy_value %0 : $T                         // user: %4
  %3 = function_ref @multiResult : $@convention(thin) <τ_0_0> (@in τ_0_0) -> (@out τ_0_0, @out τ_0_0) // user: %4
  %4 = apply %3<T>(%2) : $@convention(thin) <τ_0_0> (@in τ_0_0) -> (@out τ_0_0, @out τ_0_0) // users: %5, %7, %9
  %5 = tuple_extract %4 : $(T, T), 0              // user: %6
  %6 = copy_value %5 : $T                         // user: %10
  %7 = tuple_extract %4 : $(T, T), 1              // user: %8
  %8 = copy_value %7 : $T                         // user: %12
  destroy_value %4 : $(T, T)                      // id: %9
  destroy_value %6 : $T                           // id: %10
  destroy_value %0 : $T                           // id: %11
  return %8 : $T                                  // id: %12
}
